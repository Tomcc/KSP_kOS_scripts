RUN ONCE LIB.

FUNCTION IS_VERTICAL {
	RETURN SHIP:FACING:FOREVECTOR * SHIP:UP:FOREVECTOR > 0.7.
}

SET MIN_SPEED TO -10.

PRINT "Initiating reentry routine.".


WAIT UNTIL SHIP:VERTICALSPEED < MIN_SPEED.

PRINT "Deploying gear to dispel speed".
GEAR ON.
SAS OFF.

LOCK STEERING to -VELOCITY:SURFACE.

WAIT UNTIL IS_VERTICAL().

SET WORST_DT TO 0.
UPDATE_DT().
UNTIL FALSE {
	UPDATE_DT().
	IF DELTA_T > WORST_DT {
		SET WORST_DT TO DELTA_T.
	}

	LOCAL THROTTLE_LAG_OFFSET TO -SHIP:VERTICALSPEED * WORST_DT * 2. //actuating the thrust takes 1 tick

	// //find the downwards component of the thrust
	LOCAL VERTICAL_RATIO IS SHIP:FACING:FOREVECTOR * SHIP:UP:FOREVECTOR.
	LOCAL VERTICAL_THRUST IS VERTICAL_RATIO * AVAILABLE_THRUST().
	LOCAL CRITICAL_H IS ((SHIP:VERTICALSPEED^2) / (2 * ((VERTICAL_THRUST / MASS_KG()) - SHIP:SENSORS:GRAV:MAG))).

	IF CRITICAL_H < 1 {
		BREAK.
	}

	PRINT WORST_DT.

	IF ALT:RADAR - ROCKET_HEIGHT - THROTTLE_LAG_OFFSET <= CRITICAL_H {
		THROTTLE_TO_MAX().
	}
	ELSE {
		ADD_THROTTLE(-0.35).
	}
}

PRINT "Final speed " + SHIP:VERTICALSPEED.

LOCK STEERING TO UP.
THROTTLE_TO_TWR(0).

PRINT "Burn complete, releasing control.".
